#!/usr/bin/env perl

# This script converts TASS boundtable's (.dbt files) to 
# tab separated text table.
#
# The reason for this is tab-separated tables do not
# need a specialized parser, because split(/\t/) is
# enough and standard unix tools, like cut, sort or
# join can handle this format.

use FindBin;
use lib "$FindBin::RealBin/../perl/lib";
use File::Basename;
use strict;
use warnings;

my $CONFIG = &parse_configuration();

foreach my $file (@ARGV) {
    my $name = defined($CONFIG->name) ? $CONFIG->name : basename($file, '.dbt');
    open(FILE,"<$file") || die "Could not open file $file";
    my $number = 1;
    while (<FILE>) {
	chomp;
	next if /^\s*$/;
	my @F = split(/\t+/);
	die "ERROR: missing second column in boundtable $file at row $number.\nADD 'use strict; use warnings;' TO ALL YOUR PERL PROGRAMS!!!!!!!" unless ($#F);
	my @C = split(/\,/,$F[1]);
	foreach my $coord (@C) {
	    my @fields = split(/[\.:]+/,$coord);
	    push(@fields, $CONFIG->evalue) if (scalar(@fields) < 3);
	    print join($CONFIG->output_separator,$F[0],$name,@fields),"\n";
	}
	$number++;
    }
    close(FILE);
}

exit 0;

sub parse_configuration {
    use Application::Config qw(:argcount GetConfig);

    my $appconfig = GetConfig({ EXPAND_STDIN => 0 },

			      'all' => {
				  ACTION   => sub { my ($state, $name, $val) = @_; push(@ARGV, glob("*.dbt")) if ($val && !scalar(grep { /\.dbt$/ } @ARGV)) },
				  ALIAS    => 'a',
				  ARGCOUNT => ARGCOUNT_NONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Convert all files matching the standard boundtable filename extension (.dbt)"
			      },

			      'evalue' => {
				  ALIAS    => 'e',
				  ARGCOUNT => ARGCOUNT_ONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Add this dummy E-value to the evalue column, if missing",
			      },

			      'name' => {
				  ALIAS    => 'n',
				  ARGCOUNT => ARGCOUNT_ONE,
				  DEFAULT  => undef,
				  SUMMARY  => "Output somain name",
			      },

			      'output_separator' => {
				  ALIAS    => 'r',
				  ARGCOUNT => ARGCOUNT_ONE,
				  DEFAULT  => "\t",
				  SUMMARY  => "Set output column separator",
			      },
	);

	unshift(@ARGV, "-")  if (! -t STDIN && scalar @ARGV == 0);
	return $appconfig;
}

# POD: start

=head1 NAME

 boundtable2table - convert a TASS boundtable to a domain table

=head1 SYNOPSIS

 # Simplest usage: convert one file
 boundtable2table domain.dbt

 # Convert all .dbt files in this directory
 boundtable2table -all

=head1 DESCRIPTION

Boundtables are text tables generated by some scripts from the TASS toolkit
that represent matches of sequence models (query sequences, profiles, HMMs,
etc.) to segments of protein sequences. 

This program converts boundtables to a domain table, i.e. TAB-separated
text tables that represent each match to a sequence on a different row,
instead of compacting all the information for each protein in a single
row.

=head1 AUTHOR

 Robson Francisco de Souza

=cut
# POD: end
