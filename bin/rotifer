#!/usr/bin/env perl

# LIBRARIES and PRAGMAS: start
use FindBin;
use lib "$FindBin::RealBin/../perl/lib";
use File::Basename;
use IPC::Run qw(run);
use Pod::Perldoc;
use Rotifer;
use Rotifer::DB::Base;
use Rotifer::Utils qw(describe_subclasses aoa2txt);
use strict;
use warnings;
# LIBRARIES and PRAGMAS: end

my $CONFIG = &parse_configuration();

# Version
if ($CONFIG->version) {
    print STDERR "Rotifer Toolkit (version $Rotifer::VERSION)\n";
    exit 0;
}

# Check request and do something
if ($CONFIG->data) {
    print join("\n",Rotifer::DB::Base->data_path(@ARGV)),"\n";
}

elsif ($CONFIG->documentation) {
    my %commands = map { (basename($_),$_) } glob("$Rotifer::RealBaseDir/bin/*");
    @ARGV = ('rotifer') unless (scalar @ARGV);
    if (my @cmd = grep { exists $commands{$_} } @ARGV) {
	foreach my $arg (@ARGV) {
	    run([ $arg, "--help" ]) if (-x $commands{$arg});
	}
    } elsif (my @libs = grep { !exists $commands{$_} } @ARGV) {
	push(@libs,1) if (scalar(@libs) % 2);
	Pod::Perldoc->run(@libs);
    }
}

elsif ($CONFIG->environment) {
    @ARGV = ('all') unless (scalar @ARGV);
    foreach my $arg (@ARGV) {
	my $variable = "ROTIFER_".uc($arg);
	if (exists $ENV{$variable}) {
	    print  $ENV{$variable},"\n";
	} elsif ($arg eq 'all') {
	    print aoa2txt([qw/name value/], map { [ $_, $ENV{$_} ] } sort keys %ENV);
	} elsif (exists $ENV{$arg}) {
	    print  $ENV{$arg},"\n";
	}
    }
}

elsif ($CONFIG->library) {
    @ARGV = ("Rotifer") unless (scalar @ARGV);
    print describe_subclasses(@ARGV);
}

elsif ($CONFIG->tool) {
    @ARGV = ("*") if (!scalar @ARGV || grep { /^(all|\*)$/i } @ARGV);
    my @cmd = grep { -r $_ } glob(join(" ",map { "$FindBin::RealBin/$_" } @ARGV));
    if (scalar @cmd) {
	print describe_subclasses(@cmd);
    } else {
	die "Rotifer component not found";
    }
};

# Exit
exit 0;
# MAIN PROGRAM: end

###############
# Subroutines

# Creating the configuration object
sub parse_configuration {
    use Application::Config qw(:argcount GetConfig);

    my $had_args = scalar(@ARGV);
    my $appconfig = GetConfig({ EXPECT_INPUT   => 0,
				IGNORE_UNKNOWN => 1,
			      },

			      'data' => {
				  ARGCOUNT => ARGCOUNT_NONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Find local databases or local data directories seen by Rotifer.",
			      },

			      'documentation' => {
				  ALIAS    => 'doc',
				  ARGCOUNT => ARGCOUNT_NONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Show documentation about Rotifer components and/or scripts. When this option is active, all options supported by perldoc are also available.",
			      },

			      'environment' => {
				  ALIAS    => 'env',
				  ARGCOUNT => ARGCOUNT_NONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Print information about the setup of Rotifer tools in your system.",
			      },

			      'library' => {
				  ALIAS    => 'lib',
				  ARGCOUNT => ARGCOUNT_NONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Print a summary of all Rotifer libraries",
			      },

			      'tool' => {
				  ALIAS    => 'cmd',
				  ARGCOUNT => ARGCOUNT_NONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Print a summary of all Rotifer tools",
			      },

			      'version' => {
				  ALIAS    => 'v',
				  ARGCOUNT => ARGCOUNT_NONE,
				  DEFAULT  => 0,
				  SUMMARY  => "Print Rotifer's version number.",
			      },
	);
    $appconfig->_pod(1) unless (scalar @ARGV || $had_args);

    return $appconfig;
}

# POD: start

=head1 NAME

ROTIFER: Rapid Open-source Tools and Infrastructure For
         data Exploration and Research

=head1 SYNOPSIS

 # To see the documentation
 rotifer

 # To get a summary of all Rotifer tools
 rotifer --tool

 # or
 rotifer -cmd

=head1 DESCRIPTION

RoTIfeR is a somewhat arbitrary collection of tools for data analysis.
It is also an infrastructure for the development of new data analysis 
and data management tools.

Technically, RoTIfeR intends to provide a high-level application 
development framework for computational analysis of biological sequences
and a collection of tools based on this framework.

In pratical terms it is just a collection of Perl scripts and 
libraries I wrote for my bioinformatics research and latter organized
and systematized. IOW, it is just my way of doing things.

This script is an interface to Rotifer's fundamental libraries and can
be used to retrieve information on Rotifer's execution environment,
documentation, etc.

See "rotifer --doc Rotifer" for more details.

=head1 AUTHOR

 Robson Francisco de Souza

=cut
# POD: end
