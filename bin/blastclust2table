#!/usr/bin/env perl

use FindBin;
use lib "$FindBin::RealBin/../perl/lib";
use warnings;
use strict;

# Parse configuration/options
my $CONFIG = &parse_configuration();
my $sep = $CONFIG->delimiter;

my $i = 0;
while (<>) {
    chomp;
    next if /Start clustering of \d+ queries/;
    next if /Finished processing/;
    my @F = split(/$sep/);
    my @extra = ();
    foreach my $apply (@{ $CONFIG->apply }) {
	push(@extra, $apply->(@F));
    }
    map { print join("\t", $_, $i, scalar(@F), @extra),"\n" } @F;
    $i++;
}

exit 0;

###############
# Subroutines

sub compile {
    my $code = shift;
    my $ref = eval "$code";
    die "Error while compiling user defined subroutine:\n$@" if ($@);
    return $ref;
}

# See perldoc Application::Config for details
sub parse_configuration {
    use Application::Config qw(:argcount GetConfig);
    my $appconfig = GetConfig(# Hash
			      'apply' => {
				  ALIAS    => 'a',
				  DEFAULT  => [],
				  ARGCOUNT => ARGCOUNT_LIST,
				  SUMMARY  => "Code defining anonymous subroutine that executes for each input file row. Receives an array with a copy of the current input file's row data",
			      },
			      'delimiter' => {
				  ALIAS    => 's',
				  DEFAULT  => " ",
				  ARGCOUNT => ARGCOUNT_ONE,
				  SUMMARY  => "String to use when splitting cluster rows (perl regexp)",
			      },
	);

    # Compiling user defined subroutines
    my $array = $appconfig->apply;
    map { $array->[$_] = compile($array->[$_]) } 0..$#{$array};

    return $appconfig;
}

# POD: start
# Documentation (use option -doc or --doc to see it!)
#
# AppConfig::AutoDoc can automatically extract Plain Old Documentation
# from within the caller program's, add descriptions of the options
# created by L<define> and do some pretty formatting for output.
# 
# Note that POD may be added anywhere in your program, thus allowing 
# the documentation for a program to be left side by side with the
# function's definition.

=head1 NAME

 blastclust2table - convert blastclust output to a table

=head1 SYNOPSIS

 blastclust2table file1 file2...

=head1 DESCRIPTION

Convert NCBI's BLASTCLUST output from to a TAB-separated table

=head1 AUTHOR

 Robson Francisco de Souza

=cut
# POD: end
